// GVLOGO 2.00 (c) G. VASSEUR
// programme de phrases al√©atoires

// travail sur les listes d'√©l√©ments

pour detruis_tout
// d√©ruit les phrases apprises
  donne "sujets []
  donne "verbes []
  donne "objets []
  donne "circons []
fin

pour tire_au_hasard :groupe
// tire au hasard un √©l√©ment
  si vide? :groupe [rends []]
  rends item hasard compte :groupe :groupe
fin

pour un_circons
// renvoie un compl√©ment circonstanciel
  rends tire_au_hasard :circons
fin

pour un_objet
// renvoie un compl√©ment d'objet
  rends tire_au_hasard :objets
fin

pour un_verbe
// renvoie un verbe
  rends tire_au_hasard :verbes
fin

pour un_sujet
// renvoie un sujet
  rends tire_au_hasard :sujets
fin

// manipulation des groupes

pour corrige :groupe
// corrige le groupe
  si vide? :groupe [ecris [Liste vide !] rends []]
  ecris sd :groupe
  rends sd :groupe
fin

pour montre :groupe
// montre le groupe
  si vide? :groupe [ecris [Liste vide !]] sinon [ecris :groupe]
fin

pour nouveau :groupe
// ajoute ‡ une liste
  rends mp ll :groupe
fin

pour efface :groupe
// mise √† z√©ro d'un groupe
  locale "choix
  ecris [Etes-vous s√ªr ? O/N]
  donne "choix lis.car
  si ou egal? :choix "O egal? :choix "o [ecris [destruction effectu√©e...] rends []] sinon [ecris [Abandon] rends :groupe]
fin

// applications particuli√®res des manipulations

pour montre_tout
// montre les listes actuelles
  locale "choix
  vt
  gras vrai ecris [Sujets :] gras faux
  m_sujets
  ecris []
  gras vrai ecris [Verbes :] gras faux
  m_verbes
  ecris []
  gras vrai ecris [Compl√©ments d'objet :] gras faux
  m_objets
  ecris []
  gras vrai ecris [Compl√©ments circonstanciels :] gras faux
  m_circons
  ecris []
  donne "choix lis.car
fin

pour m_sujets
// renvoie les sujets
  montre :sujets
fin

pour m_verbes
// renvoie les verbes
  montre :verbes
fin

pour m_objets
// renvoie les compl√©ments d'objet
  montre :objets
fin

pour m_circons
// renvoie les compl√©ments circonstanciels
  montre :circons
fin

pour corrige_sujets
// corrige les sujets
  donne "sujets corrige :sujets
fin

pour corrige_verbes
// corrige les verbes
  donne "verbes corrige :verbes
fin

pour corrige_objets
// corrige les compl√©ments d'objet
  donne "objets corrige :objets
fin

pour corrige_circons
// corrige les compl√©ments circonstanciels
  donne "circons corrige :circons
fin

pour une_phrase
// construit une phrase
  rends phrase phrase phrase un_sujet un_verbe un_objet un_circons
fin

pour nouveau_sujet
// ajoute un sujet
  donne "sujets nouveau :sujets
fin

pour nouveau_verbe
// ajoute un verbe
  donne "verbes nouveau :verbes
fin

pour nouveau_objet
// ajoute un complÈment d'objet
  donne "objets nouveau :objets
fin

pour nouveau_circons
// ajoute un compl√©ment circonstanciel
  donne "circons nouveau :circons
fin

pour efface_sujets
// efface les sujets
  donne "sujets efface :sujets
fin

pour efface_verbes
// efface les verbes
  donne "verbes efface :verbes
fin

pour efface_objets
// efface les objets
  donne "objets efface :objets
fin

pour efface_circons
// efface les compl√©ments circonstanciels
  donne "circons efface :circons
fin

// programme principal

pour aide
// aide en ligne
  locale "choix
  vt
  gras vrai italique  vrai ecris [AIDE POUR ECRILOGO] gras faux italique faux
  ecris []
  gras vrai ecris [Nouveaux Groupes] gras faux
  ecris [permet de constituer des stocks de sujets, verbes, compl√©ments d'objet et compl√©ments circonstanciels.]
  gras vrai ecris [Ecriture de phrases] gras faux
  ecris [g√©n√®re automatiquement des phrases √† partir des groupes.]
  gras vrai ecris [Destruction] gras faux
  ecris [permet de d√©truire des groupes.]
  gras vrai ecris [Correction] gras faux
  ecris [permet de corriger des groupes.]
  gras vrai ecris [Voir les groupes] gras faux
  ecris [permet de visualiser les groupes constitu√©s.]
  donne "choix lis.car
fin

pour ecrire
// programme principal
  debuter
  travailler
  au_revoir
fin

pour debuter
// commencer le programme
  prepare_ecran
  detruis_tout
  bienvenue
fin

pour prepare_ecran
// pr√©pare l'√©cran de travail
  vt
  fcft 4
  fct 7
fin

pour bienvenue
// message de bienvenue
  ecris []
  gras vrai ecris [Bienvenue dans ECRILOGO !] gras faux
  ecris []
fin

pour travailler
// boucle principale
  locale "choix
  gras vrai ecris [TRAVAILLER AVEC ECRILOGO] gras faux
  ecris [1 Nouveaux groupes]
  ecris [2 Ecriture de phrases]
  ecris [3 Remise √† z√©ro]
  ecris [4 Correction]
  ecris [5 Voir les groupes]
  ecris [6 Aide]
  ecris [7 Fin]
  donne "choix lis.car
  si  membre? :choix [1 2 3 4 5 6 7]  [execute item :choix [[Nouveau_Quoi][ vt Des_Phrases] [Efface_Quoi][Corrige_Quoi][Montre_Tout][Aide][Stop]]]
  vt Travailler
fin

pour nouveau_quoi
// ajoute une partie
  locale "choix
  vt
  gras vrai ecris "AJOUT gras faux
  ecris []
  tape [1 SUJET ] tape compte :sujets ecris []
  tape [2  VERBE ] tape compte :verbes ecris []
  tape [3 COMPLEMENT D'OBJET ] tape compte :objets ecris []
  tape [4 COMPLEMENT CIRCONSTANCIEL ] tape compte :circons ecris []
  ecris [5 Fin]
  donne "choix lis.car
  ecris []
 ecris [Tapez votre texte puis ENTREE]
  si membre? :choix [1 2 3 4 5] [execute item  :choix [[nouveau_sujet][nouveau_verbe][nouveau_objet][nouveau_circons][stop]]]
  nouveau_quoi
fin

pour corrige_quoi
// corrige une partie
  locale "choix
  vt
  gras vrai ecris "CORRECTION gras faux
  ecris []
  tape [1 SUJET (] tape compte :sujets tape [)] ecris []
  tape [2  VERBE (] tape compte :verbes tape [)] ecris []
  tape [3 COMPLEMENT D'OBJET (] tape compte :objets tape [)] ecris []
  tape [4 COMPLEMENT CIRCONSTANCIEL (] tape compte :circons tape [)] ecris []
  ecris [5 Fin]
  donne "choix lis.car
  ecris []
  si membre? :choix [1 2 3 4 5] [execute item  :choix [[corrige_sujets][corrige_verbes][corrige_objets][corrige_circons][stop]]]
  corrige_quoi
fin

pour efface_quoi
// efface une liste
  locale "choix
  vt
  gras vrai ecris "EFFACEMENT gras faux
  ecris[]
  tape [1 SUJET ] tape compte :sujets ecris []
  tape [2  VERBE ] tape compte :verbes ecris []
  tape [3 COMPLEMENT D'OBJET ] tape compte :objets ecris []
  tape [4 COMPLEMENT CIRCONSTANCIEL ] tape compte :circons ecris []
  ecris [5 Fin]
  donne "choix lis.car
  ecris []
  si membre? :choix [1 2 3 4 5] [execute item  :choix [[efface_sujets][efface_verbes][efface_objets][efface_circons][stop]]]
  efface_quoi
fin

pour des_phrases
// invente des phrases
  locale "choix
  donne "choix une_phrase
  si vide? :choix [ecris [Pas de groupes disponibles !] donne "choix lis.car stop]
  ecris :choix
  ecris []
  ecris [Tapez F pour FIN]
  donne "choix lis.car
  si ou egal? :choix "f egal? :choix "F [stop]
  des_phrases
fin

pour au_revoir
// fin de l'application
  vt
  ecris [A bient√¥t, peut-√™tre !]
fin

ecrire
